pluginManagement {
	repositories {
		maven {
			name = "Fabric"
			url = "https://maven.fabricmc.net/"
		}
		maven {
			name = "Jitpack"
			url = "https://jitpack.io"
		}
		mavenCentral()
		gradlePluginPortal()
	}
	resolutionStrategy {
		eachPlugin {
			switch (requested.id.id) {
				case "com.replaymod.preprocess": {
					useModule("com.github.Fallen-Breath:preprocessor:${requested.version}")
					break
				}
			}
		}
	}
}

def mainVersion = "1.21.4"

include(":$mainVersion")

def mainProj = project(":$mainVersion")
mainProj.projectDir = file("versions/$mainVersion")
mainProj.buildFileName = "../../common.gradle"

if (!System.env.JITPACK) {
	def versions = Arrays.asList(
			"1.21.1"
	)
	for (String version : versions) {
		include(":$version")

		def proj = project(":$version")
		proj.projectDir = file("versions/$version")
		proj.buildFileName = "../../common.gradle"
	}

	include(":wrapper")
}